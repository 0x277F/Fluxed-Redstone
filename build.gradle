buildscript {
    ext.kotlin_version = '1.0.0-beta-4589'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "kotlin"

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven {
        name = "Modmuss50"
        url = "http://maven.modmuss50.me/"
    }
	maven {
		name = "Amadornes"
		url "http://maven.amadornes.com/" 
	}
    mavenCentral()
}

version = "1.1.0"

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

minecraft {
    version = "1.8.9-11.15.0.1718"
    mappings = 'snapshot_20160103'
    replace "@MODVERSION@", project.version
    makeObfSourceJar = false
    useDepAts = true
    runDir = "run"
}

group = 'FluxedRestone'

dependencies {
    compile 'RebornCore:RebornCore-1.8.9:1.4.0.+:dev'
	deobfCompile 'MCMultiPart:MCMultiPart:1.0.5:universal'
    shade "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    shade "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

reobfJar { 
    addExtraSrgLine "PK: kotlin kotlinRepackFluxedRedstone" 
	addExtraSrgLine "PK: org/jetbrains org/jetbrainsRepackFluxedRedstone"
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest {
        attributes 'FMLCorePlugin': 'io.drakon.forgelinFR.CorePlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "FluxedRestone"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'FluxedRestone'
                    url 'https://github.com/modmuss50/Fluxed-Redstone'
                    scm {
                        url 'https://github.com/modmuss50/Fluxed-Redstone'
                        connection 'scm:git:git@github.com:modmuss50/Fluxed-Redstone.git'
                        developerConnection 'scm:git:git@github.com:modmuss50/Fluxed-Redstone.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/modmuss50/Fluxed-Redstone/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://raw.githubusercontent.com/modmuss50/Fluxed-Redstone/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
	configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
    manifest {
        attributes 'FMLCorePlugin': 'io.drakon.forgelinFR.CorePlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

artifacts {
    archives deobfJar
}

build.dependsOn deobfJar

task wrapper(type: Wrapper) {
    //To update the wrapper change the version bellow then run: gradle wrapper
    gradleVersion = '2.9'
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
